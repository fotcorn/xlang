?start: translation_unit

translation_unit: (struct_def | function_def)* // enum_def

function_param: IDENTIFIER":" type
function_params: function_param | function_params "," function_param
function_def: IDENTIFIER "(" function_params? ")" (":" type)? code_block

// enum_entry: IDENTIFIER","
// enum_def: "enum" IDENTIFIER "{" enum_entry+ "}"

struct_entry: IDENTIFIER":" type ","
struct_def: "struct" IDENTIFIER "{" struct_entry+ "}"

IDENTIFIER: /[a-zA-Z_][a-zA-Z_0-9]*/
INTEGER: (/[1-9][0-9]*/ | "0" )
STRING_LITERAL: /"[^"]*"/
array_access: "[" compare_expr "]"
var_access: IDENTIFIER array_access? ("." var_access)?

code_block: "{" statement* "}"

?statement: loop | if_statement | (function_call ";") | variable_dec | variable_def | variable_assign | control ";"
loop: "loop" code_block
if_statement: "if" "(" compare_expr ")" code_block

variable_dec: IDENTIFIER ":" type ";"
variable_def: IDENTIFIER ":" type "=" compare_expr ";"
variable_assign: var_access "=" compare_expr ";"
!control: ("break" | "continue" | "return" compare_expr?)

function_call: IDENTIFIER "(" (compare_expr? | compare_expr ("," compare_expr)*) ")"
integer_constant: INTEGER
string_literal: STRING_LITERAL
!boolean_literal: ("false" | "true")
?primary_expression: var_access | function_call | integer_constant | string_literal | boolean_literal | "(" compare_expr ")"

!?mul_div_expr: primary_expression | (mul_div_expr "*" primary_expression) | (mul_div_expr "/" primary_expression) | (mul_div_expr "%" primary_expression)
!?add_sub_expr: mul_div_expr | (add_sub_expr "+" mul_div_expr) | (add_sub_expr "-" mul_div_expr)
!?compare_expr: add_sub_expr | (compare_expr compare_operator add_sub_expr)

!?compare_operator: "==" | "!=" | ">=" | ">" | "<" "<="

!type: IDENTIFIER | "[" IDENTIFIER "]"

%import common.WS
%import common.CPP_COMMENT
%import common.C_COMMENT

%ignore WS
%ignore CPP_COMMENT
%ignore C_COMMENT
